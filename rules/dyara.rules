# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

rule dream_yara_index_ibf_update:
    input:
        old_filter="{prefix}.{indexer}_{meth}_{num_bins}.filter",
        symbolic="{prefix}_{meth}_{num_bins}_up/ref_up_{num_bins}"
    output:
        "{prefix}.{indexer}_{meth}_{num_bins}_up.filter"
    wildcard_constraints:
        num_bins="\d+."
    params:
        num_threads=config["num_threads"],
        idir="{prefix}_{meth}_{num_bins}_up/"
    log:
        "{prefix}.dyara_{meth}_{num_bins}.ibf_up.log",
    shell:
        "cp {input.old_filter} {output}; "
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_update_filter  --threads {params.num_threads} {output} {params.idir}*.fasta 2> {log};"

rule dream_yara_index_fm_update:
    input:
        "{prefix}_{meth}_{num_bins}_up/ref_up_{num_bins}"
    output:
        log="{prefix}.dyara_{meth}_{num_bins}.index_up.log",
        odir="{prefix}.dyara_{meth}_{num_bins}.indices_up/"
    params:
        idir="{prefix}_{meth}_{num_bins}_up/",
        misc=config["dindexers"]["dyara"]
    shell:
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_indexer_dis {params.misc} -o {output.odir} {params.idir}/*.fasta &> {output.log}; "


rule dream_yara_index_ibf:
    input:
        "{prefix}_{meth}_{num_bins}/ref_{num_bins}"
    output:
        "{prefix}.{indexer}_{meth}_{num_bins}.filter"
    wildcard_constraints:
        num_bins="\d+."
    params:
        num_bins="{num_bins}",
        idir="{prefix}_{meth}_{num_bins}/",
        misc=lambda wildcards: config["ibf_indexers"][wildcards.indexer]
    log:
        "{prefix}.dyara_{meth}_{num_bins}.ibf.log",
    shell:
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_build_filter -b {params.num_bins} {params.misc} -o {output} {params.idir} 2> {log};"

rule dream_yara_index_fm:
    input:
        "{prefix}_{meth}_{num_bins}/ref_{num_bins}"
    output:
        log="{prefix}.dyara_{meth}_{num_bins}.index.log",
        odir="{prefix}.dyara_{meth}_{num_bins}.indices/"
    params:
        idir="{prefix}_{meth}_{num_bins}/",
        misc=config["dindexers"]["dyara"]
    shell:
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_indexer_dis {params.misc} -o {output.odir} {params.idir}/*.fasta &> {output.log}; "



rule dream_yara_map_se:
    input:
        ibf="{prefix}/{reference}.dyara_{meth}_{num_bins}.filter",
        index="{prefix}/{reference}.dyara_{meth}_{num_bins}.index.log",
        read="{prefix}/{reads}.fastq"
    wildcard_constraints:
        config="[a-zA-Z0-9_]+"
    output:
        "{prefix}/{reads}.{reference}.dyara_{config}_{meth}_{num_bins}.bam"
    log:
        "{prefix}/{reads}.{reference}.dyara_{config}_{meth}_{num_bins}.bam.log"
    params:
        idir="{prefix}/{reference}.dyara_{meth}_{num_bins}.indices/",
        misc=lambda wildcards: config["dmappers"]["dyara_" + wildcards.config]
    shell:
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_mapper_dis {params.misc} -o {output} -fi {input.ibf} {params.idir} {input.read} 2> {log}; "

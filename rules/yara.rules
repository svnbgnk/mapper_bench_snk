# vim: syntax=python tabstop=4 expandtab
# coding: utf-8


rule yara_index:
    input:
        "{prefix}.fasta"
    output:
        touch("{prefix}.yara.index")
    log:
        "{prefix}.yara.index.log"
    params:
        misc=config["indexers"]["yara"]
    shell:
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_indexer {params.misc} {input} --output-prefix {output} 2> {log}"

rule yara_map_se:
    input:
        "{prefix}/{reference}.yara.index",
        "{prefix}/{reads}.fastq"
    wildcard_constraints:
        config="[a-zA-Z0-9_]+"
    output:
        "{prefix}/{reads}.{reference}.yara_{config}.bam"
    log:
        "{prefix}/{reads}.{reference}.yara_{config}.bam.log"
    benchmark:
        "{prefix}/{reads}.{reference}.yara_{config}.bam.bench"
    params:
        misc=lambda wildcards: config["mappers"]["yara_" + wildcards.config]
    shell:
        "command time -f 'RES:%e\\t%M\\t%P' "
        "./bin/yara_mapper {params.misc} --output-file {output} {input} 2> {log}"
